plugins {
	id 'java'
	id 'org.springframework.boot' version '2.7.8'
	id 'io.spring.dependency-management' version '1.0.15.RELEASE'
}

group = 'com.senla.worklog.reminder'
version = '0.0.1'
sourceCompatibility = '11'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
	mavenLocal()

	maven {
		url 'https://packages.atlassian.com/maven/repository/public'
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-aop'
	implementation 'org.liquibase:liquibase-core'
	implementation 'org.jsoup:jsoup:1.15.3'
	implementation 'org.mapstruct:mapstruct:1.5.3.Final'
	implementation 'com.senla.worklog.reminder:commons-spring-boot-starter:2.7.8'
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'org.postgresql:postgresql'
	annotationProcessor 'org.projectlombok:lombok'
	annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.3.Final'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.projectreactor:reactor-test'
	annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
}

tasks.named('test') {
	useJUnitPlatform()
}

task buildDockerImage(type: Exec) {
	workingDir "$projectDir"
	commandLine "docker", "build", ".",
			"-t", "letimof/${project.name}:${project.version}",
			"--build-arg", "PROJECT_NAME=${project.name}",
			"--build-arg", "PROJECT_VERSION=${project.version}"
}

task loginDocker(type: Exec) {
	commandLine "docker", "login", "-u", "$System.env.DOCKER_USERNAME", "-p", "$System.env.DOCKER_PASSWORD"
}

task pushDockerImage(type: Exec) {
	commandLine "docker", "push", "letimof/${project.name}:${project.version}"
}

pushDockerImage.dependsOn loginDocker
pushDockerImage.dependsOn buildDockerImage
